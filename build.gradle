plugins {
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.5.0'
    id 'java'
    id 'java-library'
    id 'maven'
}

repositories {
    mavenCentral()

    maven {
        url = uri('http://packages.confluent.io/maven/')
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'io.confluent:kafka-avro-serializer:7.2.1'
    implementation 'org.apache.avro:avro:1.11.1'
    implementation 'io.cloudevents:cloudevents-kafka:2.4.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.apache.kafka:kafka_2.12:2.7.0'

    compileOnly 'org.projectlombok:lombok:1.18.24'
	annotationProcessor 'org.projectlombok:lombok:1.18.24'
}

group = 'io.github.kattlo'
version = '0.11.0'
description = 'CloudEvent Apache Kafka® - Avro™ Serializer'

java.sourceCompatibility = JavaVersion.VERSION_11

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
	useJUnitPlatform()
}

avro {
    fieldVisibility = "PRIVATE"
    stringType = "CharSequence"
}

sourceSets.main.java.srcDirs += 'build/generated-main-avro-java'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
    }
  }
}

wrapper {
    gradleVersion = "6.1.1"
    distributionType = Wrapper.DistributionType.ALL
}
